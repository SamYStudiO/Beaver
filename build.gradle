buildscript {
    ext {
        versions = [
                compileSdk          : 28,
                minSdk              : 21,
                targetSdk           : 28,

                // kotlin
                kotlin              : '1.3.10',

                // android
                core_ktx            : '1.0.1',
                appcompat           : '1.0.2',
                constraintlayout    : '1.1.3',
                material            : '1.0.0',
                lifecycle           : '2.0.0',
                navigation          : '1.0.0-alpha07',

                // firebase
                firebase_core       : '16.0.5',
                firebase_appindexing: '16.0.2',
                firebase_perf       : '16.2.0',
                crashlytics         : '2.9.6',

                // dagger
                dagger              : '2.16',
                assisted_inject     : '0.3.2',

                // reactive
                rxjava              : '2.2.4',
                rxandroid           : '2.1.0',
                rxkotlin            : '2.3.0',
                rxbinding           : '3.0.0-alpha1',
                rxpreferences       : '2.0.0',
                rxpicasso           : '0.3.0',

                // network
                okhttp              : '3.12.0',
                retrofit            : '2.5.0',
                picasso             : '3.0.0-SNAPSHOT',

                // debug
                timber              : '4.7.1',
                leakcanary          : '1.6.2',

                // misc
                android_state       : '1.4.1',

                // test
                junit               : '4.12',
                test_core           : '1.0.0',
                robolectric         : '4.0.2',
                mockito             : '2.23.4',
                runner              : '1.1.0',
                espresso_core       : '3.1.0'
        ]
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'com.google.firebase:firebase-plugins:1.1.5'
        classpath 'io.fabric.tools:gradle:1.26.1'
        classpath 'android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0-alpha07'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

